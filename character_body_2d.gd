extends CharacterBody2D

const tile_size: Vector2 = Vector2(50, 50)
var sprite_node_pos_tween: Tween

@onready var body = $Body
@onready var animated_sprite = $Body/AnimatedSprite2D

func _ready() -> void:
	animated_sprite.play("idle")

func _physics_process(delta: float) -> void:
	if !sprite_node_pos_tween or !sprite_node_pos_tween.is_running():
		if Input.is_action_pressed("ui_up") and !$up.is_colliding():
			_move(Vector2(0, -1))
		elif Input.is_action_pressed("ui_down") and !$down.is_colliding():
			_move(Vector2(0, 1))
		elif Input.is_action_pressed("ui_left") and !$left.is_colliding():
			_move(Vector2(-1, 0)) 
		elif Input.is_action_pressed("ui_right") and !$right.is_colliding():
			_move(Vector2(1, 0))
		else:
			if animated_sprite.animation != "idle":
				animated_sprite.play("idle")

func _move(dir : Vector2):
	global_position += dir * tile_size
	body.global_position -= dir * tile_size
	
	if animated_sprite.animation != "walk":
		animated_sprite.play("walk")
		
	if dir.x != 0:
		animated_sprite.flip_h = dir.x < 0
		
	if sprite_node_pos_tween:
		sprite_node_pos_tween.kill()
		
	sprite_node_pos_tween = create_tween()
	sprite_node_pos_tween.set_process_mode(Tween.TWEEN_PROCESS_PHYSICS)
	sprite_node_pos_tween.tween_property(body, "global_position", global_position, 0.185).set_trans(Tween.TRANS_LINEAR)
			
